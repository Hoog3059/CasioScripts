'ProgramMode:RUN

'Real code starts here

#Region Variables

'WormHeadPos
3->X
3->Y

'FoodPos
7->P
7->Q

'Worm Length
3->D

'Direction: N:0 E:1 Z:2 W:3
2->B

'counter
0->A

'previous positions
{10,2}->Dim Mat A
3->Mat A[1,1]
3->Mat A[1,2]
3->Mat A[2,1]
2->Mat A[2,2]
3->Mat A[3,1]
1->Mat A[3,2]

#End Region

#Region Gameloop

'Gameloop Updates every 10 clock times
While 0=0
    If D=10
    Then 
    Menu "YOU WIN!","YAHEE!",1,"AWESOME!",1
    IfEnd

    'Find direction
    Getkey->C

    If C=28
        Then 0->B
    IfEnd

    If C=27
        Then 1->B
    IfEnd

    If C=37
        Then 2->B
    IfEnd

    If C=38
        Then 3->B
    IfEnd

    A+1->A
    If A=13-D
        Then 
            0->A
            ClrText

            If B=0
            Then 
                If Y-1<1
                    Then 7->Y
                Else Y-1->Y
                IfEnd
            IfEnd

            If B=1
            Then 
                If X+1>21
                    Then 1->X
                Else X+1->X
                IfEnd
            IfEnd

            If B=2
            Then 
                If Y+1>7
                    Then 1->Y
                Else Y+1->Y
                IfEnd
            IfEnd

            If B=3
            Then 
                If X-1<1
                    Then 21->X
                Else X-1->X
                IfEnd
            IfEnd

            If X=P And Y=Q
            Then 
                D+1->D
                RanInt#(1,21)->P
                RanInt#(1,7)->Q
            Else Locate P,Q,"o"
            IfEnd

            Locate X,Y,"_#E6A6_"

            {10,2}->Dim Mat B
            For 1->I To D-1
            Locate Mat A[I,1],Mat A[I,2],"_#E6A6_"
            Mat A[I,1]->Mat B[I+1,1]
            Mat A[I,2]->Mat B[I+1,2]
            Next

            Mat B->Mat A
            X->Mat A[1,1]
            Y->Mat A[1,2]
    IfEnd
WhileEnd

Lbl 1
Stop
#End Region